#!perl

## Test helper - runs bucardo_ctl while avoiding testing/forking problems

use strict;
use warnings;
use Time::HiRes qw/sleep gettimeofday tv_interval/;

my $CTL = 'blib/script/bucardo_ctl';
## Test things out
-e $CTL or die qq{Could not find "$CTL": $!\n};

## Don't run if it looks like another process is already running
if ($^O =~ /Win/) {
	warn "Warning: Cannot check for prior instances on this platform\n";
}
elsif (!-x '/bin/ps') {
	warn q{Warning: No "/bin/ps" found to check for prior instances};
}
else {
	my $found = 0;
	for (split /\n/ => qx{/bin/ps w}) {
		next if /^\s*$$\s/;
		/perl $0\b/ and $found = $_;
	}
	if ($found) {
		warn qq{Not starting $0, already one or more instances running: $found\n};
		exit;
	}
}

my $controlfile = 'bucardo_test_control';
unlink $controlfile;

exit if fork;
{
	if (-e $controlfile) {
		open my $fh, '<', $controlfile or die qq{Could not open "$controlfile": $!\n};
		my $commands = <$fh>;
		close $fh or warn qq{Could not close "$controlfile": $!\n};
		system "$CTL $commands";
		unlink $controlfile;
	}

	sleep 0.5;
	redo;
}
